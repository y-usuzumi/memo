# 有关派生

本文是以GHC 8.6.1为蓝本。参考了[http://downloads.haskell.org/~ghc/latest/docs/users_guide.pdf](http://downloads.haskell.org/~ghc/latest/docs/users_guide.pdf)中有关章节。


## 与派生 (deriving) 相关的扩展

Haskell 98允许在数据类型声明中加入派生子句来生成特定类型类 (class) 的标配实例。GHC从以下几个方面进行了扩展。

* 派生机制可以与类型声明分离 (`-XStandaloneDeriving`)
* 通过若干扩展可以派生除`Eq`、`Ord`、`Enum`、`Ix`、`Bounded`、`Read`和`Show`以外的类型类。
* 除了通过生成所有方法定义进行派生的标配方式之外，还支持两种GHC特殊的派生策略，可以对任意类型类派生出实例：
  * `-XGeneralizedNewtypeDeriving`
  * `-XDeriveAnyClass`

用户可以声明所需的派生策略，尤其是在编译器的默认选择是错误的情况下。当然也可以不加声明。


### 对空数据类型进行派生

_-XEmptyDataDeriving_ >= 8.4.1

允许为空类型派生出标准类型类的实例。

使用`-XEmptyDataDecls`可以定义无构造函数的数据类型。通过该扩展，可以为这些数据类型派生出类型类实例，比如：

```haskell
data Empty deriving (Eq, Ord, Read, Show)
```

派生出的代码如下：

```haskell
instance Eq Empty where
  _ == _ = True

instance Ord Empty where
  compare _ _ = EQ

instance Read Empty where
  readPrec = pfail
  
instance Show Empty where
  showsPrec _ x = case x of {}_
```

注意，该扩展只在派生上述四个标准类型类时才需要，使用其他扩展进行的派生操作不需要同时启用该扩展，包括：

* `-XStandaloneDeriving`
* 具有与自己一一对应的扩展的某些类型类，比如`-XDeriveFunctor`
* `-XDeriveAnyClass`


### 为派生语句推断的上下文

比如：

```haskell
data T0 f a = MkT0 a         deriving( Eq )
data T1 f a = MkT1 (f a)     deriving( Eq )
data T2 f a = MkT2 (f (f a)) deriving( Eq )
```

自然生成的`Eq`代码如下：

```haskell
instance Eq a         => Eq (T0 f a) where ...
instance Eq (f a)     => Eq (T1 f a) where ...
instance Eq (f (f a)) => Eq (T2 f a) where ...
```

T0完全OK，T1也还OK，但是T2不是Haskell 98标准，而且可能导致实例无法终止。

GHC采取了保守策略：接收前两个，拒绝第三个。规则如下：

* 在推断出的实例上下文中出现的约束 (Constraint)，仅包含类型变量，且类型变量最多出现一次。

该规则无视flag。如果你想要更变态的上下文，可以通过`-XStandaloneDeriving`自己写一个。


### 独立的派生声明

_-XStandaloneDeriving_ >= 6.8.1

允许独立于类型声明的`deriving`声明。

开启`-XStandloneDeriving`后，可以使用独立的`deriving`声明：

```haskell
data Foo a = Bar a | Baz String

deriving instance Eq a => Eq (Foo a)
```

除了`deriving`关键字和缺少`where`子句，其他部分与一般的实例声明完全相同。

独立的派生声明与`deriving`子句有若干重要区别：

* 独立的派生声明与类型声明不必在同一模块之中（谨防出现孤儿实例）
* 多数情况下，使用独立的派生声明和一般的实例声明一样，必须显式提供上下文，而`deriving`子句可以进行上下文推断。
  例外：当启用`-XPartialTypeSignatures`时，可以使用`_`占位来通知GHC进行推断，如：
  ```haskell
  deriving instance _ => Eq (Foo a)
  ```
* 启用`-XFlexibleInstances`的情况下，使用独立的派生声明时可以提供比所声明的数据类型更具体的实例类型，如：
  ```haskell
  data Foo a = Bar a | Baz String
  
  deriving instance Eq a => Eq (Foo [a])
  deriving instance Eq a => Eq (Foo (Maybe a))
  ```
  只会生成`Foo [a]`和`Foo (Maybe a)`的实例。
* 独立的派生声明并不限制类型声明，GHC会生成样板代码并作类型检查。好处在于可以为GADT或其他怪异的数据类型派生实例，比如：
  ```haskell
  data T a where
    T1 :: T Int
    T2 :: T Bool
    
  deriving instance Show (T a)
  ```
  这种情况下你不能使用`deriving`子句，因为不支持GADT。
  缺点在于，如果生成的样板代码类型检查失败，报错信息会提示在生成的代码，而不是你写的代码上。
* 大多数情况下，使用独立的派生声明时，所有的构造函数都必须在作用域内，因为后台生成样板代码需要使用这些信息。
  例外：启用`-XDeriveAnyClass`的情况下，上述条件非必需，因为`DeriveAnyClass`仅会生成空实例。
  
其他的场合上，独立的派生声明与`deriving`子句没有区别。


### 除标准类型类以外可以直接派生的类型类

在Haskell 98中能够通过`deriving`子句进行派生的只有`Eq`、`Ord`、`Enum`、`Ix`、`Bounded`、`Read`、`Show`。

GHC在其基础上又加了如下类型类：

* 通过`-XDeriveGeneric`，可以派生`GHC.Generics`模块中的`Generic和Generic1`，可以进行“Generic Programming”。
* 通过`-XDeriveFunctor`，可以派生`GHC.Base`模块中的`Functor`实例。
* 通过`-XDeriveDataTypeable`，可以派生`Data.Data`模块中的`Data`实例。
* 通过`-XDeriveFoldable`，可以派生`Data.Foldable`模块中的`Foldable`实例。
* 通过`-XDeriveTraversable`，可以派生`Data.Traversable`模块中的`Traversable`实例。由于`Traversable`要求`Functor`和`Foldable`实例，你可能也希望同时派生后两者，因此`DeriveTraversable`强制启用`DeriveFunctor`和`DeriveDataTypeable`
* 通过`-XDeriveLift`，可以派生`template-haskell`包下`Language.Haskell.TH.Syntax`模块中的`Lift`实例。

也可以使用独立的派生声明方式。
必须把要派生的类型类带入作用域中才可以在`deriving`子句中使用。

#### 派生Functor实例

_-XDeriveFunctor_ >= 7.10.1

自动派生`Functor`实例。

启用后，可以为kind是`Type -> Type`的数据类型自动派生`Functor`实例，如：

```haskell
data Example a = Ex a Char (Example a) (Example Char)
               deriving Functor
```

生成如下实例：

```haskell
instance Functor Example where
  fmap f (Ex a1 a2 a3 a4) = Ex (f a1) a2 (fmap f a3) a4
```

派生逻辑是，遍历每个构造函数，根据参数类型应用一个mapping函数：

* 如果参数是一个类型变量，并且语法上与该类型的最后一个类型参数相同（上例中的`a`），则把`f`应用于该参数。
* 如果参数语法上不同于该类型的最后一个类型参数，但其中有对该类型参数的引用，则递归应用`fmap`。
* 如果参数中不包含该类型的最后一个类型参数，则保持原数据不变。

逻辑中的第二条中有一些坑，比如下例是可以编译通过的：

```haskell
newtype Right a = Right (Either Int a) deriving Functor
```

而下例是不能编译通过的：

```haskell
newtype Wrong a = Wrong (Either a Int) deriving Functor
```

原因涉及到类型中最后一个类型变量（`a`）出现的位置。在`Right`中，`a`作为`Either Int a`中的最后一个参数出现，而在`Wrong`中，`a`并未作为最后一个参数出现。

想象一下，Funtor实例需要把`Wrong a`转换为`Wrong b`，对应的构造函数是`Wrong (Either b Int)`，然而`Either a Int`如何转换为`Either b Int`？靠`Functor`是做不到的。

因此通用规则是：

如果最后一个类型参数出现在数据声明的右边，那么它必须满足以下条件之一：

1. 裸地出现（如：`newtype Id a = Id a`）。
2. 作为类型构造函数的最后一个参数出现。

_除非_：

1. 出现在元组中。当一个非unit元组出现在数据声明的右边，将其视作各个类型的积，也就是说，对于下面的代码：

   ```haskell
   newtype Triple a = Triple (a, Int, [a]) deriving Functor
   ```
   
   将会生成如下实例：
   
   ```haskell
   instance Functor Triple where
     fmap f (Triple a) = Triple (case a of
                                   (a1, a2, a3) -> (f a1, a2, fmap f a3))
   ```
   
   也就是说，和`Triple a = Triple a Int [a]`基本相当。
   
1. 出现在函数类型中。最后一个类型参数可以出现在函数类型的任意协变位置上，如：

   ```haskell
   newtype CovFun1 = CovFun1 (Int -> a) deriving Functor
   newtype CovFun2 = CovFun2 ((a -> Int) -> a) deriving Functor
   newtype CovFun3 = CovFun3 (((Int -> a) -> Int) -> a) deriving Functor
   ```
   
   上述代码可以正确编译，但：
   
   ```haskell
   newtype ContraFun1 a = ContraFun1 (a -> Int) deriving Functor
   newtype ContraFun2 a = ContraFun2 ((Int -> a) -> Int) deriving Functor
   newtype ContraFun3 a = ContraFun3 (((a -> Int) -> a) -> Int) deriving Functor
   ```
   
   上述代码无法编译。
   
   所谓的协变和逆变，从直观上理解，协变类型是产出，逆变类型是消费。Haskell中大多数类型是协变的，然而函数类型中箭头左侧的可变性是反向的。比如类型为`a -> b`的函数出现在协变位置上，则`a`是逆变位置，而`b`是协变位置。类似地，类型为`a -> b`的函数出现在逆变位置上，则`a`是协变位置，`b`是逆变位置。
   
   为什么当最后一个类型参数处于逆变位置时无法派生出`Functor`的实例？
   
   我们假设存在`ContraFun1`的`Functor`实例存在，它应该长这样：
   
   ```haskell
   instance Functor ContraFun1 where
     fmap f (ContraFun1 g) = ContraFun1 (\x -> _)
   ```
   
   我们手里只有`f :: a -> b`，`g :: a -> Int`和`x :: b`，如何仅用这三个函数来填充下划线的部分，得到`Int`类型的结果呢？
   
   答案是没有。
   
   相反，当最后一个类型参数处于协变位置时，有如下可能：
   
   ```haskell
   instance Functor CovFun1 where
     fmap f (CovFun1 g) = CovFun1 (\x -> f (g x))
     
   instance Functor CovFun2 where
     fmap f (CovFun2 g) = CovFun2 (\h -> f (g (\x -> h (f x))))
     
   instance Functor CovFun3 where
     fmap f (CovFun3 g) = CovFun3 (\h -> f (g (\k -> h (\x -> f (k x)))))
   ```

还有其他几种情况无法派生`Functor`实例：

* 无参数数据类型，如`data Nothing = Nothing`
* 数据类型中最后一个类型参数用于`DatatypeContexts`约束中，如`data Ord a => O a = O a`
* 数据类型中最后一个类型参数用于`ExistentialQuantification`约束中，或在GADT中进行进一步处理，如：
  ```haskell
  data T a b where
    T4 :: Ord b => b -> T a b
    T5 :: b -> T b b
    T6 :: T a (b, b)
    
  deriving instance Functor (T a)
  ```
  无法通过编译。
  
当最后一个类型参数具有phantom角色时，`Functor`实例不再用常规算法实现，而是进行强制转换 (coerce)，如：

```haskell
data Phantom a = Z | S (Phantom a) deriving Functor
```

将生成以下实例：

```haskell
instance Functor Phantom where
  fmap _ = coerce
```

当没有构造函数时，派生的`Functor`实例强制使用`EmptyCase`创建bottom值，如：

```haskell
data V a deriving Functor
type role V nominal
```

将生成以下实例：

```haskell
instance Functor V where
  fmap _ z = case z of
```
