# 异常处理

> 注：本文是对[Exceptions Best Practices in Haskell](https://www.fpcomplete.com/blog/2016/11/exceptions-best-practices-haskell)的随兴翻译。


## The IO contract

Haskell的社区中流传的一个常见的观点是：异常应该显式地指定在类型中；异步异常是辣鸡。

然而通过GHC的实现我们可以看出它的立场：

* IO中可以抛出任何类型的异常，只要是Exception的实例。
* GHC支持异步异常。

我们假定GHC的思路是正确的，这不为过吧？

请默默地认同如下观点：

* IO中的任何代码都可以抛出运行时异常。
* 可以通过异步异常在任何时间任何地点杀掉任何线程。


## 各种辣鸡反模式

### ExceptT IO

形如下面的代码：

```haskell
myFunction :: String -> ExceptT MyException IO Int
```

至少有三个猫饼：

1. 无法组合。如果别人有个不同的异常类型`HisException`，就不能简单地组合。
1. 它给人造成一个假象：这个函数里只会抛出`MyException`类型的异常。其实任何IO操作都可以
   抛出不同类型的异常，甚至是异步异常。
1. 并没有限制住异常的种类，而是提供了另一种抛出异常的方式，既可以`throwE`，又可以`liftIO . throwIO`。

所以，用`ExceptT`/`EitherT`/`ErrorT`来套`IO`几乎永远是错误的。

另外，在公共API中提供具体类型的monad transformer也几乎永远是错误的。最好是提供基于mtl和
类型类的抽象类型。

还有一种长得差不多的：

```haskell
myFunction :: String -> ExceptT Text IO Int
```

编码者觉得以后`Text`可能会改成其他的异常类型，比如`MyException`，然而，很大可能性你这个`Text`会留一辈子，因为它刚好避免了之前提到的组合问题。然而用非结构化的`Text`来表达有用的错误类型信息你不觉得很213么。

解决办法是：尽量返回`Either`，对于少见的错误才抛出异常。

另外要提到一点，`ExceptT`和非IO-base的monad组合没有任何问题。


### Mask-them-all

这种反模式的背景是：在每个地方处理异步异常是处理不过来/不干净的，干脆隐藏掉算了。

异步异常是很烦，但却是保证系统稳定可靠运行的关键。

就算很烦，你也要处理。

掌握以下几个最佳实践：

* [Bracket模式](https://wiki.haskell.org/Bracket_pattern)
* [safe-exceptions包](http://hackage.haskell.org/package/safe-exceptions)
* 如果你的控制流极其复杂，或者使用non-linear scoping of resources (这特么是啥意思)，使用[resourcet](https://hackage.haskell.org/package/resourcet)


## 最佳方案

### MonadThrow

考虑以下代码：

```haskell
foo <- lookup "foo" m
bar <- lookup "bar" m
baz <- lookup "baz" m
f foo bar baz
```

如果这个函数返回`Nothing`，我们根本不知道是哪里出问题，可能是如下原因之一：

1. "foo"不在map里
1. "bar"不在map里
1. "baz"不在map里
1. `f`函数返回了`Nothing`

所以`Maybe`返回类型会导致丢失很多有用信息。再比较下面的代码：

```haskell
lookup :: Eq k => k -> [(k, v)] -> Either (KeyNotFound k) v

f :: SomeVal -> SomeVal -> SomeVal -> Either F'sExceptionType F'sResult
```

这样我们需要的信息就都保留了。然而新的问题来了：这两个函数没法组合。

这时候你就需要思考：我到底要不要这些信息？如果不需要，用`Maybe`就可以了，否则，
请使用`exceptions`包中的`MonadThrow`：

```haskell
lookup :: (MonadThrow m, Eq k) => k -> [(k, v)] -> m v

f :: MonadThrow m => SomeVal -> SomeVal -> SomeVal -> m F'sResult
```

和`Either`版本相比，我们丢失了错误类型信息，相应地，我们得到了与`Maybe`（以及其他`MonadThrow`实例）的可组合性和一致性。

`MonadThrow`是一个折衷方案，但它是经过作者深思熟虑的，也和Haskell的运行时异常保持了一致，都不会捕捉异常类型信息。


### Transformers

下面的类型签名约束太强：

```haskell
foo :: Int -> IO String
```

可以泛化为：

```haskell
foo :: MonadIO m => Int -> m String
```

再借助`liftIO`来使用。

这可以让函数与任何IO-base的transformer组合使用。

然而，如果有个函数长这样：

```haskell
bar :: FilePath -> (Handle -> IO a) -> IO a
```

如果你想把这个函数参数放在一个IO-base的transformer中，你会发现很难搞。可以使用`lift-base`，但是很复杂。
你可以使用`safe-exceptions`库得到这种更为泛化的类型：

```haskell
bar :: (MonadIO m, MonadMask m) => FilePath -> (Handle -> m a) -> m a
```

不止可以用于异常处理，还可以用于创建线程之类。另外可以考虑一下使用`resourcet`库中的`Acquire`类型。


### 自定义异常类型

这是一种很213的实践：

```haskell
foo = do
    if x then return y else error "something bad happened"
```

这让调用者的处理变得很困难。相比之下，你应该自定义一个异常类型：

```haskell
data SomethingBad = SomethingBad
    deriving Typeable
instance Show SomethingBad where
    show SomethingBad = "something bad happened"
instance Exception SomethingBad
foo = do
    if x then return y else throwM SomethingBad
```

现在给调用者抓`SomethingBad`就很容易了。另外，通过使用`throwM`，代码执行顺序也可以得到充分保证，
而不像使用`error`，它只会创建一个pure值异常，只有对异常求值的时候才会确实地抛出异常。

之于样板代码。。。额。。。忍忍吧。


## 为什么说GHC在异常的处理方式方面的设计是好的设计

我在Reddit上作过如下评论：

> I don't believe there is a better solution to sync exceptions, actually.
> That's because most of the time I see people complaining about IO throwing exceptions,
> what they really mean is "this specific exception just bit me,
> why isn't this exception explicit in the type signature?"

进一步澄清我的观点：

* 基本上没有任何一个IO操作能保证100%成功。
* 如果每个IO操作都返回`IO (Either UniqueExceptionType a)`，编程模型将变得极其繁琐。同时当`a`为`()`时，
  还让人容易忘记做异常检测。
* 反过来，如果每个IO操作都返回`IO (Either SomeException a)`，至少我们不用处理一堆互相打架的异常类型，
  也可以使用`ErrorT`来简化代码，但。。。
* 这不相当于又重新设计了当前的`IO`嘛。

我觉得人们忽略了一个事实：IO中隐含着“该操作可能会失败”的含义，所有的场景都是。从类型上你看不出来，但
你潜意识里要知道这件事。另外，运行时异常比`ErrorT`效率更高。

虽然很多人抱怨异常处理多么麻烦，但`ErrorT`或其他东西能不能简化处理过程也是个未知数。


## 实例：readLine

需求如下：如何设计一个函数从标准输入读入一行内容并解析？

我们先从一个更简单的问题开始：如果只是解析一个`String`，该怎么做？

我们需要定义一个异常告诉我们哪部分字符串没有正确解析，以及原本要解析成什么类型：

```haskell
#!/usr/bin/env stack
-- stack --resolver lts-7.8 runghc --package safe-exceptions
{-# OPTIONS_GHC -Wall -Werror #-}
import Control.Exception.Safe (Exception, MonadThrow, SomeException, throwM)
import Data.Typeable          (TypeRep, Typeable, typeRep)
import Text.Read              (readMaybe)

data ReadException = ReadException String TypeRep
  deriving (Typeable)

instance Show ReadException where
  show (ReadException s typ) = concat
    [ "Unable to parse as "
    , show typ
    , ": "
    , show s
    ]

instance Exception ReadException

readM :: (MonadThrow m, Read a, Typeable a) => String -> m a
readM s = res
  where
    res =
      case readMaybe s of
        Just x -> return x
        Nothing -> throwM $ ReadException s (typeRep res)

main :: IO ()
main = do
  print (readM "hello" :: Either SomeException Int)
  print (readM "5" :: Either SomeException Int)
  print (readM "5" :: Either SomeException Bool)

  -- Also works in plain IO
  res1 <- readM "6"
  print (res1 :: Int)
  res2 <- readM "not an int"
  print (res2 :: Int) -- will never get called
```

这个函数可以适配多种monad，同时异常是有明确意义的。

现在我们想读入标准输入，有两种选择：

* `readLine1 :: (MonadIO m, MonadThrow n, Read a, Typeable a) => m (n a)`
  这种签名想表达的含义是：异常经常发生，所以不想和处理IO的monad混在一起。
* `readLine2 :: (MonadIO m, MonadThrow m, Read a, Typeable a) => m a`
  这种签名把IO和异常monad混在一起，相当于是说：“我们不想频繁处理异常，
  如果要处理的话你得显式地使用`tryAny`或类似的方法。实践中，没必要同时使用
  `MonadIO`和`MonadThrow`，因为你可以通过`liftIO`来组合它们。这样签名就变成了：
  `readLine2 :: (MonadIO m, Read a, Typeable a) => m a`

具体用哪种很大程度上决定于个人喜好。前者在异常方面更加显式，但是我个人会倾向于使用第二种，
因为第一种和`ExceptT`套`IO`一样，给人一种函数内部把失败情况都处理干净了的错觉。

两种方式用代码表述如下：

```haskell
#!/usr/bin/env stack
-- stack --resolver lts-7.8 runghc --package safe-exceptions
{-# OPTIONS_GHC -Wall -Werror #-}
import Control.Exception.Safe (Exception, MonadThrow, SomeException, throwM)
import Control.Monad          (join)
import Control.Monad.IO.Class (MonadIO, liftIO)
import Data.Typeable          (TypeRep, Typeable, typeRep)
import Text.Read              (readMaybe)

data ReadException = ReadException String TypeRep
  deriving (Typeable)

instance Show ReadException where
  show (ReadException s typ) = concat
    [ "Unable to parse as "
    , show typ
    , ": "
    , show s
    ]

instance Exception ReadException

readM :: (MonadThrow m, Read a, Typeable a) => String -> m a
readM s = res
  where
    res =
      case readMaybe s of
        Just x -> return x
        Nothing -> throwM $ ReadException s (typeRep res)

readLine1 :: (MonadIO m, MonadThrow n, Read a, Typeable a) => m (n a)
readLine1 = fmap readM (liftIO getLine)

-- Without the usage of liftIO here, we'd need both MonadIO and
-- MonadThrow constraints.
readLine2 :: (MonadIO m, Read a, Typeable a) => m a
readLine2 = liftIO (join readLine1)

main :: IO ()
main = do
  putStrLn "Enter an Int (non-runtime exception)"
  res1 <- readLine1
  print (res1 :: Either SomeException Int)
  putStrLn "Enter an Int (runtime exception)"
  res2 <- readLine2
  print (res2 :: Int)
```
